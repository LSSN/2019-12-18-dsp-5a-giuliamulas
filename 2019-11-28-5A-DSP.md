# 5A - NOME COGNOME

## Verifica DSP - 18/12/2019

### PROGRAMMAZIONE

![diagramma](https://github.com/LSSN/2019-12-18-DSP-5A/blob/master/DIAGRAMMA.png)

Dal diagramma di flusso estratto dalla partitura di Risonanze Erranti di Luigi Nono
è stata realizzata la ricostruzione del sistema informatico necessario per l'esecuzione
dal vivo del brano.

![porting](https://github.com/LSSN/2019-12-18-DSP-5A/blob/master/process.svg)

Scrivi il relativo codice faust collegando i blocchi di codice messi a disposizione
considerando il diagramma di flusso realizzato e la relativa interfaccia grafica.

![gui](https://github.com/LSSN/2019-12-18-DSP-5A/blob/master/GUI.png)

A scrittura ultimata registrare un estratto audio che ne testi l'operatività.

```
// completa il codice

import("stdfaust.lib");

// ----------------------------------------------------------------- INSTRUMENTS
contr = vgroup("[01] CONTRALTO", chstrip <: hgroup("[90] CONTRALTO", (*(fader) : svmeter), sends));
flaut = vgroup("[02] FLAUTI", chstrip <: hgroup("[90] FLAUTI", (*(fader) : svmeter), sends));
btuba = vgroup("[03] TUBA", chstrip <: hgroup("[90] TUBA", (*(fader) : svmeter), sends));
csard = vgroup("[04] CAMPANE SARDE", chstrip <: hgroup("[90] CAMPANE SARDE", (*(fader) : svmeter), sends));
bongo = vgroup("[05] BONGOS", chstrip <: hgroup("[90] BONGOS", (*(fader) : svmeter), sends));
crota = vgroup("[06] CROTALI", chstrip <: hgroup("[90] CROTALI", (*(fader) : svmeter), sends));

// ----------------------------------------------------------------------- SENDS
adel = checkbox("[01] DELAY") : si.smoo;
are4 = checkbox("[02] REVERB 4 SEC") : si.smoo;
ar80 = checkbox("[03] REVERB 10~80 SEC") : si.smoo;
ahar = checkbox("[04] HARMONIZER") : si.smoo;
aha1 = checkbox("[05] HALAPHON 1") : si.smoo;
aha2 = checkbox("[06] HALAPHON 2") : si.smoo;
aha3 = checkbox("[07] HALAPHON 3") : si.smoo;

sends = vgroup("[99] SENDS" , *(adel), *(are4), *(ar80), *(ahar), *(aha1), *(aha2), *(aha3)) ;

// ----------------------------------------------------------------- ELECTRONICS
// ---------------------------------------------------------------------- DELAYS
fbgroup(x) = hgroup("[11]Feedback Delay", x);

fbgain1 = fbgroup(vslider("Fb 1", 0.,0.,1.,0.1) : si.smoo);
fbgain2 = fbgroup(vslider("Fb 3", 0.,0.,1.,0.1) : si.smoo);
fbgain3 = fbgroup(vslider("Fb 5", 0.,0.,1.,0.1) : si.smoo);
fbgain4 = fbgroup(vslider("Fb 7", 0.,0.,1.,0.1) : si.smoo);

delbank = _ <: (+<: de.delay(ba.sec2samp(5.0),ba.sec2samp(5.0)), de.delay(ba.sec2samp(5.5),ba.sec2samp(5.5)))~*(fbgain1),
               (+<: de.delay(ba.sec2samp(6.2),ba.sec2samp(6.2)), de.delay(ba.sec2samp(6.6),ba.sec2samp(6.6)))~*(fbgain2),
               (+<: de.delay(ba.sec2samp(7.3),ba.sec2samp(7.3)), de.delay(ba.sec2samp(7.7),ba.sec2samp(7.7)))~*(fbgain3),
               (+<: de.delay(ba.sec2samp(8.2),ba.sec2samp(8.2)), de.delay(ba.sec2samp(9.1),ba.sec2samp(9.1)))~*(fbgain4):
               _, ro.cross(2), _, _, ro.cross(3) : si.bus(5), ro.cross(2), _; // route the delayed signal to 1 3 2 4 5 7 8 6

// ----------------------------------------------------------------- HARMONIZER

harmonizer = vgroup("[10]HARMONIZER", ef.transpose(
  hslider("window (samples)", 1000, 50, 10000, 1),
  hslider("xfade (samples)", 10, 1, 10000, 1),
  hslider("shift (cents) ", 0, -2600, +100, 0.01))
	);

// -----------------------------------------------------------------  DIRECT

direct = _;

fader	= vslider("[01] Volume", -96, -96, +12, 0.1) : ba.db2linear : si.smoo ;
instruments = si.bus(18) <: hgroup("[01] INSTRUMENTS", csard, bongo, crota) ;

process = instruments;

//------------------------------------------------ INPUT CHANNEL GENERIC CONTROL
//------------------------------------------------ SELECT THE HARDWARE INTERFACE
chstrip = vgroup("[01] INPUT CHANNEL", ff800 : inpeq);

inpeq = vgroup("[03] CH SETUP", *(gain) :
        *(rpol) :
        ba.bypass1(lop,fi.lowpass(2,hcut)) :
        ba.bypass1(hip,fi.highpass(2,lcut)) :
        ba.bypass1(eq, (fi.low_shelf(LL, FL) :
                        fi.peak_eq(LP1, FP1, BP1) :
                        fi.peak_eq(LP2, FP2, BP2) :
                        fi.high_shelf(LH, FH)))) with{
  // GROUPS
  pregroup(x) = hgroup("[03] PRE SECTION", x);
  pg_group(x) = pregroup(vgroup("[01] PHASE and GAIN", x));
  lc_group(x) = pregroup(vgroup("[02] LOW CUT", x));
  hc_group(x) = pregroup(vgroup("[03] HI CUT", x));
  eq_group(x) = vgroup("[04] PARAMETRIC EQ",x);
  hs_group(x) = eq_group(hgroup("[01] High Shelf", x));
  pq1_group(x) = eq_group(hgroup("[02] Band", x));
  pq2_group(x) = eq_group(hgroup("[03] Band", x));
  ls_group(x) = eq_group(hgroup("[04] Low Shelf",x));
  // REVERSE POLARITY AND GAIN
  rpol = pg_group(1 - ((checkbox("[01] Reverse Phase")*(2))));
  gain = pg_group(vslider("[02] Gain [unit:dB] [style:knob]", 0, -24, +24, 0.1)) : ba.db2linear : si.smoo;
  // PRE FILTERS
  lop = hc_group(1 - checkbox("[03] HiCut"));
  hcut = hc_group(vslider("[04] High Cut [unit:Hz] [style:knob]", 8000, 20, 20000, 0.1)) : si.smoo;
  hip = lc_group(1 - checkbox("[01] LoCut"));
  lcut = lc_group(vslider("[02] Low Cut [unit:Hz] [style:knob]", 500, 20, 20000, 0.1)) : si.smoo;
  // PARAMETRIC EQ - TWEAKED FROM FAUST DEMOS
  eq = eq_group(1 - checkbox("[00] EQ ON"));
	LH = hs_group(vslider("[00] Gain [unit:dB] [style:knob]", 0,-36,36,.1) : si.smoo);
	FH = hs_group(vslider("[01] Freq [unit:Hz] [style:knob]", 8000,100,19999,1) : si.smoo);
	LP1 = pq1_group(vslider("[00] Gain 1 [unit:dB] [style:knob]",0,-36,36,0.1) : si.smoo);
	FP1 = pq1_group(vslider("[01] Freq 1 [unit:Hz] [style:knob]", 2500,0,20000,1)) : si.smoo;
	Q1  = pq1_group(vslider("[02] Q 1 [style:knob] [scale:log]",40,1,1000,0.1) : si.smoo);
	BP1 = FP1/Q1;
	LP2 = pq2_group(vslider("[00] Gain 2 [unit:dB] [style:knob]", 0,-36,36,0.1));
	FP2 = pq2_group(vslider("[01] Freq 2 [unit:Hz] [style:knob]", 500,0,20000,1)) : si.smoo;
	Q2  = pq2_group(vslider("[02] Q 2 [style:knob] [scale:log]", 40,1,1000,0.1) : si.smoo);
	BP2 = FP2/Q2;
	LL = ls_group(vslider("[00] Gain [unit:dB] [style:knob]", 0,-36,36,0.1) : si.smoo);
	FL = ls_group(vslider("[01] Freq [unit:Hz] [style:knob]", 200,20,5000,1): si.smoo);
};

//----------------------------------------------- OUTPUT CHANNEL GENERIC CONTROL
//------------------------------------------------------------------ OUT CHANNEL
out(c) = hgroup("[99] OUT%o", vgroup("[97]", outdel : outfbank) : *(gain) : svmeter)
  with{
    o = c+(1);
    gain = vslider("[98] Gain [unit:dB]", 0, -70, +12, 0.01) : ba.db2linear : si.smoo;
    //------------------------------------------------------------ 8-BAND GRAPHIC EQ
    mth_octave_filterbank_demo(O) = ba.bypass1(fon, mthoctavefilterbankdemo)
    with{
      eq_group(x) = vgroup("[98] EQ", x);
      fon = eq_group(1 - (checkbox("[01] EQ ON")));
    	M = O;
    	N = 8*M; // total number of bands (highpass band, octave-bands, dc band)
    	ftop = 10000;
    	mthoctavefilterbankdemo = chan;
    	chan = fi.mth_octave_filterbank_default(M,ftop,N) : sum(i,N,(*(ba.db2linear(fader(N-i)))));
    	fader(i) = eq_group(vslider("[02] Band%2i [unit:dB][style:knob]", 0, -70, 12, 0.1)) : si.smoo;
    };
    outfbank = mth_octave_filterbank_demo(1); // octave-bands = default
    //----------------------------------------- OUT DELAY FOR LOPUDSPEALER ALINGMENT
    outdel = ba.bypass1(don, de.delay(maxdel, del))
      with{
        maxdel = 192000;
        del = int(ba.sec2samp(nentry("[97] Delay [unit:ms]", 0, 0, 1, 0.001)));
        don = 1 - (checkbox("[96] DEL ON"));
      };
  };

//------------------------------------------------------------------ SEAM METERS
svmeter(x) = attach(x, an.amp_follower(0.150, x) : ba.linear2db : vbargraph("[99][unit:dB]", -70, +6));
shmeter(x) = attach(x, an.amp_follower(0.150, x) : ba.linear2db : hbargraph("[99][unit:dB]", -70, +6));

//-------------------------------------------- RME FireFace 800 - INPUT SELECTOR
//-------------------------------------------------------------------- 18 INPUTS
ff800 = ba.selectn(18,channel) : hmeter : _
  with{
    hmeter(x)		= attach(x, an.amp_follower(0.150, x) : ba.linear2db : hbargraph("[02] INPUT LEVEL [unit:dB]", -70, +6));
    envelop = abs : max ~ -(1.0/ma.SR) : max(ba.db2linear(-70)) : ba.linear2db;
    channel = nentry("[01] RME FF800 - Input Channel Selector
                    [style:menu{'Analog IN 1':0;
                    'Analog IN 2':1;
                    'Analog IN 3':2;
                    'Analog IN 4':3;
                    'Analog IN 5':4;
                    'Analog IN 6':5;
                    'Analog IN 7':6;
                    'Analog IN 8':7;
                    'Analog IN 9':8;
                    'Analog IN 10':9;
                    'ADAT IN 1':10;
                    'ADAT IN 2':11;
                    'ADAT IN 3':12;
                    'ADAT IN 4':13;
                    'ADAT IN 5':14;
                    'ADAT IN 6':15;
                    'ADAT IN 7':16;
                    'ADAT IN 8':17}]", 0, 0, 18, 1) : int;
                  };
```

# Ajo!
